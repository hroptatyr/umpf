%{
typedef struct umpf_attr_s *umpf_attr_t;

typedef enum {
	/* must be first */
	UMPF_ATTR_UNK,
	/* alphabetic list of tags */
	UMPF_ATTR_ACCT,
	UMPF_ATTR_AMT,
	UMPF_ATTR_APPL_REQ_ID,
	UMPF_ATTR_APPL_REQ_TYP,
	UMPF_ATTR_BIZ_DT,

	UMPF_ATTR_CCY,
	UMPF_ATTR_CFI,
	UMPF_ATTR_CONTENT_TYPE,
	UMPF_ATTR_DTLS,
	UMPF_ATTR_EMAIL,

	UMPF_ATTR_ID,
	UMPF_ATTR_LONG,
	UMPF_ATTR_MAT_DT,
	UMPF_ATTR_MMY,
	UMPF_ATTR_QTY,

	UMPF_ATTR_QTY_DT,
	UMPF_ATTR_R,
	UMPF_ATTR_REF_APPL_ID,
	UMPF_ATTR_REG_STAT,
	UMPF_ATTR_REQ_ID,

	UMPF_ATTR_REQ_TYP,
	UMPF_ATTR_RPT_ID,
	UMPF_ATTR_RSLT,
	UMPF_ATTR_S,
	UMPF_ATTR_SCHEMA_LOCATION,

	UMPF_ATTR_SET_SES_ID,
	UMPF_ATTR_SETTL_DT,
	UMPF_ATTR_SHORT,
	UMPF_ATTR_SIDE,
	UMPF_ATTR_SRC,

	UMPF_ATTR_STAT,
	UMPF_ATTR_SYM,
	UMPF_ATTR_TOT_RPTS,
	UMPF_ATTR_TRANS_TYP,
	UMPF_ATTR_TRD_DT,

	UMPF_ATTR_TXN_TM,
	UMPF_ATTR_TXT,
	UMPF_ATTR_TYP,
	UMPF_ATTR_V,
	UMPF_ATTR_XMLNS,

	UMPF_ATTR_XR,
	UMPF_ATTR_XV,

} umpf_aid_t;

%}
%7bit
%readonly-tables
%struct-type
%define slot-name attr
%define hash-function-name __aid_hash
%define lookup-function-name __aiddify
%null-strings
/* we need this one as we plan on passing parts of a string */
%compare-strncmp
%enum

struct umpf_attr_s {
	const char *attr;
	umpf_aid_t aid;
};

%%
Acct,	UMPF_ATTR_ACCT
Amt,	UMPF_ATTR_AMT
ApplReqID,	UMPF_ATTR_APPL_REQ_ID
ApplReqTyp,	UMPF_ATTR_APPL_REQ_TYP
BizDt,	UMPF_ATTR_BIZ_DT
Ccy,	UMPF_ATTR_CCY
CFI,	UMPF_ATTR_CFI
Dtls,	UMPF_ATTR_DTLS
Email,	UMPF_ATTR_EMAIL
ID,	UMPF_ATTR_ID
Long,	UMPF_ATTR_LONG
MMY,	UMPF_ATTR_MMY
MatDt,	UMPF_ATTR_MAT_DT
Qty,	UMPF_ATTR_QTY
QtyDt,	UMPF_ATTR_QTY_DT
R,	UMPF_ATTR_R
RefApplID,	UMPF_ATTR_REF_APPL_ID
RegStat,	UMPF_ATTR_REG_STAT
ReqID,	UMPF_ATTR_REQ_ID
ReqTyp,	UMPF_ATTR_REQ_TYP
RptID,	UMPF_ATTR_RPT_ID
Rslt,	UMPF_ATTR_RSLT
SetSesID,	UMPF_ATTR_SET_SES_ID
SettlDt,	UMPF_ATTR_SETTL_DT
Short,	UMPF_ATTR_SHORT
Side,	UMPF_ATTR_SIDE
Stat,	UMPF_ATTR_STAT
Src,	UMPF_ATTR_SRC
Sym,	UMPF_ATTR_SYM
TotRpts,	UMPF_ATTR_TOT_RPTS
TransTyp,	UMPF_ATTR_TRANS_TYP
TrdDt,	UMPF_ATTR_TRD_DT
TxnTm,	UMPF_ATTR_TXN_TM
Txt,	UMPF_ATTR_TXT
Typ,	UMPF_ATTR_TYP
content-type,	UMPF_ATTR_CONTENT_TYPE
r,	UMPF_ATTR_R
s,	UMPF_ATTR_S
schemaLocation,	UMPF_ATTR_SCHEMA_LOCATION
v,	UMPF_ATTR_V
xmlns,	UMPF_ATTR_XMLNS
xr,	UMPF_ATTR_XR
xv,	UMPF_ATTR_XV
